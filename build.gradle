plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

// 'eclipse' plugin is used to generate native project files for Eclipse
// from this Gradle script - for those who don't have Gradle plugin installed in Eclipse
apply plugin: 'eclipse'

group = 'dev.kindlich.com.taskadapter'
version = '5.0.0.rc1-SNAPSHOT'


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    sourceCompatibility = 11
    targetCompatibility = 11
}

// turn off strict Javadoc checks in Java8 that break the build.
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
// you could say that it is better to fix all Javadoc issues instead, but
// they don't seem important enough to justify this.
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc).tap {
            configureEach {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.apache.httpcomponents:httpclient:4.5.14',
            'org.json:json:20231013',
            'org.slf4j:slf4j-api:1.7.25')

    testImplementation(
            'junit:junit:4.13.1',
            'org.assertj:assertj-core:3.2.0',
            'org.slf4j:slf4j-jdk14:1.7.25'
    )
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

// A PGP key is required to sign the artifacts before uploading to OSS Sonatype Maven repository.
// Put the PGP key info in your <home>/.gradle/gradle.properties file:
// signing.keyId=...
// signing.password=...
// signing.secretKeyRingFile=c:/Users/ask/.gnupg/secring.gpg
signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

publishing {
    publications {
        create("maven", MavenPublication) {
            pom {
                name = 'Redmine Java API'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                url = 'https://github.com/kindlich-dev/redmine-java-api'
                description 'Free open-source Java API for Redmine and Chiliproject bug/task management systems.' +
                        'This project was originally a part of Task Adapter application (http://www.taskadapter.com)' +
                        'Forked from https://github.com/kindlich-dev/redmine-java-api'
                developers {
                    developer {
                        name = 'kindlich'
                        email = 'kindlich@kindlich.dev'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kindlich-dev/redmine-java-api.git'
                    // "developerConnection" is required for Maven Release plugin.
                    developerConnection = 'scm:git:git@github.com:kindlich-dev/redmine-java-api.git'
                    tag = 'HEAD'
                    // A publicly browsable repository. For example, via ViewCVS
                    url = 'https://github.com/kindlich-dev/redmine-java-api/tree/master/'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/kindlich-dev/redmine-java-api/issues'
                }
            }

        }
    }


    repositories {
        maven {
            name "GitHubPackages"
            url "https://maven.pkg.github.com/kindlich-dev/redmine-java-api"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.register('packageSources', Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

tasks.register('packageJavadoc', Jar) {
    dependsOn 'javadoc'
    from javadoc.destinationDir
    archiveClassifier = 'javadoc'
}

artifacts {
    archives packageSources, packageJavadoc
}
